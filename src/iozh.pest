WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

digit = @{ ASCII_DIGIT }
letter = @{ "_" | ASCII_ALPHA }
identifier = @{ letter ~ (letter | digit)* }

doc = { "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
type_name = { identifier }
field_name = { identifier }
method_name = { identifier }


string_literal = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
integer_literal = { "-"? ~ digit+ }
literal = { string_literal | integer_literal }

type_args = { "[" ~ type_tag ~ ("," ~ type_tag)* ~ "]" }
type_tag = { type_name ~ type_args? }
type_path = { type_tag ~ ("." ~ type_tag)* }

field = { doc? ~ field_name ~ ":" ~ type_path }
fields = _{ (","? ~ field)+ }
fields_block = _{ fields | ("{" ~ fields ~ "}") | "(" ~ fields ~ ")" }

structure = { doc? ~ type_tag ~ fields_block }

method_result = { type_path }
method = { doc? ~ type_tag ~ fields_block ~ "=>" ~ method_result }
methods = _{ (","? ~ method)+ }
methods_block = _{ methods | ("{" ~ methods ~ "}") | "(" ~ methods ~ ")" }
service = { doc? ~ type_tag ~ methods_block }

choice_name = { type_tag }
choice_item_value = { doc? ~ type_tag ~ "=" ~ literal }
choice_item = { (structure | choice_item_value | (doc? ~ type_tag)) }
choice = { doc? ~ choice_name ~ "=" ~ "|"? ~ choice_item ~ ("|" ~ choice_item)* }

method_ref = { type_path }
http_route_var = @{ (":" ~ identifier) | ("{" ~ identifier ~ "}") }
http_path_part = @{ (ASCII_ALPHA | ASCII_DIGIT | "/" | "." | "-" | "_" | "~" | "!" | "$" | "&" | "'" | "(" | ")" | "+" | "," | ";" | "=" | "@" | "%")+ }
http_route_pattern = { &"/" ~ (!"*>" ~ (http_route_var | http_path_part)+)+ }
http_method = { "GET" | "PUT" | "POST" | "DELETE" | "OPTIONS" | "HEAD" | "PATCH" | "TRACE" | "CONNECT" }
http_route = { http_method ~ type_path ~ "*>" ~ http_route_pattern ~ "*>" ~ method_ref ~ fields_block }
http_routes = _{ http_route+ }
http_routes_block = _{ http_routes | ("{" ~ http_routes ~ "}") | "(" ~ http_routes ~ ")" }

http_service = { type_tag ~ http_routes_block }

project = { (http_service | structure | choice | service)+ }